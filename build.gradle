ext.springVersion = "4.0.0.RELEASE"

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    sourceCompatibility = 1.7
    version = '1.0'

    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/release/" }
        maven { url "http://repo.spring.io/milestone/" }
    }
}

project(":repositories") {
    dependencies {
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-orm:$springVersion"
        compile "org.springframework:spring-aspects:$springVersion"

        compile "org.hibernate:hibernate-entitymanager:4.3.1.Final"
        compile("org.springframework.data:spring-data-jpa:1.4.3.RELEASE") {
            exclude module: 'slf4j-api'
            exclude module: 'jcl-over-slf4j'
        }
        compile 'org.slf4j:slf4j-log4j12:1.7.1'
        compile 'com.h2database:h2:1.3.175'
        compile 'mysql:mysql-connector-java:5.1.6'

        testCompile "org.springframework:spring-test:$springVersion"
        testCompile 'junit:junit:4.11'
    }
}

project(":services") {
    dependencies {
        compile project(":repositories")
        compile "org.springframework:spring-context:$springVersion"
        compile "org.springframework:spring-aspects:$springVersion"

        compile 'org.slf4j:slf4j-log4j12:1.7.1'

        testCompile "org.springframework:spring-test:$springVersion"
        testCompile 'junit:junit:4.11'
    }
}

project(":web") {
    apply plugin: 'war'
    apply plugin: "jetty"

    httpPort = 8086

    jettyRun {
        contextPath = 'sit'
        reload = 'automatic'
        scanIntervalSeconds = 10
    }
    dependencies {
        compile project(":services")
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "org.apache.commons:commons-lang3:3.2.1"
        compile "com.fasterxml.jackson.core:jackson-databind:2.3.4"
        providedCompile "javax.servlet:javax.servlet-api:3.1.0"
        runtime 'jstl:jstl:1.2'
    }

}
